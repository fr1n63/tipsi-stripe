apply plugin: 'com.android.library'

def DEFAULT_COMPILE_SDK_VERSION             = 26
def DEFAULT_BUILD_TOOLS_VERSION             = "26.0.2"
def DEFAULT_TARGET_SDK_VERSION              = 26
def DEFAULT_GOOGLE_PLAY_SERVICES_VERSION    = "11.8.0"
def DEFAULT_FIREBASE_VERSION                = "11.8.0"
def DEFAULT_SUPPORT_LIB_VERSION             = "26.1.0"

android {
  compileSdkVersion project.hasProperty('compileSdkVersion') ? project.compileSdkVersion : DEFAULT_COMPILE_SDK_VERSION
  buildToolsVersion project.hasProperty('buildToolsVersion') ? project.buildToolsVersion : DEFAULT_BUILD_TOOLS_VERSION

  defaultConfig {
    minSdkVersion 16
    targetSdkVersion project.hasProperty('targetSdkVersion') ? project.targetSdkVersion : DEFAULT_TARGET_SDK_VERSION
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    manifestPlaceholders = [
      tipsiStripeRedirectScheme: "example"
    ]
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

dependencies {
  def supportLibVersion = project.hasProperty('supportLibVersion') ? project.supportLibVersion : DEFAULT_SUPPORT_LIB_VERSION
  def firebaseVersion = project.hasProperty('firebaseVersion') ? project.firebaseVersion : DEFAULT_FIREBASE_VERSION
  def googlePlayServicesVersion = project.hasProperty('googlePlayServicesVersion') ? project.googlePlayServicesVersion : DEFAULT_GOOGLE_PLAY_SERVICES_VERSION

  compile fileTree(include: ['*.jar'], dir: 'libs')
  compile 'com.facebook.react:react-native:+'
  compile "com.android.support:support-v4:$supportLibVersion" 
  compile "com.android.support:appcompat-v7:$supportLibVersion"
  compile "com.google.android.gms:play-services-wallet:$googlePlayServicesVersion"
  compile "com.google.firebase:firebase-core:$firebaseVersion"
  compile 'com.stripe:stripe-android:6.1.2'
  compile 'com.github.tipsi:CreditCardEntry:1.4.8.10'
}

repositories {
  maven {
    url 'https://maven.google.com/'
    name 'Google'
  }
}
